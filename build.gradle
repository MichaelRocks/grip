buildscript {
  repositories {
    jcenter()
  }
  dependencies {
    classpath 'com.jfrog.bintray.gradle:gradle-bintray-plugin:1.6'
  }
}

allprojects {
  ext.kotlinVersion = '1.0.1'

  buildscript {
    repositories {
      mavenLocal()
      jcenter()
      mavenCentral()
    }
  }

  repositories {
    mavenLocal()
    jcenter()
    mavenCentral()
  }
}

allprojects {
  def properties = new Properties()
  properties.load(new FileInputStream(new File(rootDir, "version.properties")))
  def major = properties['version.major'] as int
  def minor = properties['version.minor'] as int
  def patch = properties['version.patch'] as int
  def suffix = properties['version.suffix'].toString().trim() ?: ""
  def snapshot = properties['version.snapshot'].toBoolean()
  version = "$major.$minor.$patch" + (suffix.isAllWhitespace() ? '' : "-$suffix") + (snapshot ? '-SNAPSHOT' : '')
  group = 'io.michaelrocks'

  ext.dryRun = properties['version.dryRun'].toBoolean()
  ext.publish = properties['version.publish'].toBoolean()
}

ext {
  shadowVersion = '1.2.3'

  asmVersion = '5.0.4'
  logbackVersion = '1.1.3'

  junitVersion = '4.12'
  mockitoVersion = '2.0.31-beta'
}
